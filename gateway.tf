resource "aws_api_gateway_rest_api" "rest_api" {
  name = "docbox-rest-api"
}

resource "aws_api_gateway_resource" "proxy" {
  rest_api_id = aws_api_gateway_rest_api.rest_api.id
  parent_id   = aws_api_gateway_rest_api.rest_api.root_resource_id
  path_part   = "{proxy+}"
}

resource "aws_api_gateway_method" "proxy_method" {
  rest_api_id   = aws_api_gateway_rest_api.rest_api.id
  resource_id   = aws_api_gateway_resource.proxy.id
  http_method   = "ANY"
  authorization = "CUSTOM"
  authorizer_id = aws_api_gateway_authorizer.auth.id
}

resource "aws_api_gateway_authorizer" "auth" {
  name           = "docbox-authorizer"
  rest_api_id    = aws_api_gateway_rest_api.rest_api.id
  authorizer_uri = var.authorizer_lambda_invoke_arn
  type           = "REQUEST"

  identity_source                  = var.authorizer_identity_source
  authorizer_result_ttl_in_seconds = var.authorizer_result_ttl_in_seconds
}

resource "aws_lambda_permission" "apigw_authorizer" {
  statement_id  = "AllowExecutionFromAPIGatewayAuth"
  action        = "lambda:InvokeFunction"
  function_name = var.authorizer_lambda_function_name
  principal     = "apigateway.amazonaws.com"
  source_arn    = "${aws_api_gateway_rest_api.rest_api.execution_arn}/authorizers/${aws_api_gateway_authorizer.auth.id}"
}


resource "aws_api_gateway_integration" "lambda_integration" {
  rest_api_id             = aws_api_gateway_rest_api.rest_api.id
  resource_id             = aws_api_gateway_resource.proxy.id
  http_method             = aws_api_gateway_method.proxy_method.http_method
  type                    = "AWS_PROXY"
  integration_http_method = "POST"
  response_transfer_mode  = "STREAM"
  uri                     = module.http_lambda.response_streaming_invoke_arn
}

resource "aws_api_gateway_deployment" "deployment" {
  rest_api_id = aws_api_gateway_rest_api.rest_api.id

  lifecycle {
    create_before_destroy = true

    replace_triggered_by = [
      aws_api_gateway_resource.proxy,
      aws_api_gateway_method.proxy_method,
      aws_api_gateway_integration.lambda_integration
    ]
  }
}

resource "aws_api_gateway_stage" "default" {
  deployment_id = aws_api_gateway_deployment.deployment.id
  rest_api_id   = aws_api_gateway_rest_api.rest_api.id
  stage_name    = "default"
}

resource "aws_lambda_permission" "apigw_backend" {
  statement_id  = "AllowExecutionFromAPIGatewayBackend"
  action        = "lambda:InvokeWithResponseStreaming"
  function_name = module.http_lambda.function_name
  principal     = "apigateway.amazonaws.com"
  source_arn    = "${aws_api_gateway_rest_api.rest_api.execution_arn}/*/*"
}
